plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.13'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'jacoco'
    id "io.sentry.jvm.gradle" version "6.0.0"
}

jar {
    enabled = false
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

group = 'com.dnd'
version = '0.0.1-SNAPSHOT'
description = '소중한 순간을 기록하고 공유하는 서비스, 찍어줄게(Jjigeojulge) 백엔드 서버'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.14"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Database
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation "com.h2database:h2"
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // HTTP Client
    implementation 'org.apache.httpcomponents.client5:httpclient5'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    implementation(platform("io.sentry:sentry-bom:8.31.0"))
    implementation("io.sentry:sentry-spring-boot-starter-jakarta")

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL (Spring Boot 3.x / Jakarta 지원)
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true // 테스트 실패 시 상세 로그 출력
    }
}

checkstyle {
    configFile = file("${rootProject.projectDir}/checkstyle/naver-checkstyle-rules.xml") // 규칙 파일 위치
    configProperties = ["suppressionFile": "${rootProject.projectDir}/checkstyle/naver-checkstyle-suppressions.xml"]
}

tasks.withType(Checkstyle).configureEach {
    exclude("**/generated/**")
    exclude("**/build/**")

    reports {
        xml.required = true
        html.required = true
    }
}

test {
    jacoco {
        destinationFile = layout.buildDirectory.file('jacoco/jacocoTest.exec').get().asFile
        classDumpDir = layout.buildDirectory.dir('jacoco/classpathdumps').get().asFile
    }
    systemProperty 'spring.profiles.active', 'test'
    finalizedBy 'jacocoTestReport'
}

def jacocoExcludes = [
        '**/config/**',
        '**/generated/**',
        '**/*Application*',
        '**/exception/**',
        '**/*Exception*',
        '**/dto/**',
        '**/*Request*',
        '**/*Response*',
        '**/example/**'
]

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: jacocoExcludes)
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: jacocoExcludes)
        }))
    }

    violationRules {
//        rule {
//            limit {
//                counter = 'LINE'
//                value = 'COVEREDRATIO'
//                minimum = 0.70
//            }
//        }
    }
}
